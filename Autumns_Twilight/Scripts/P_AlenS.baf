// Hotkey configurable
IF
HotKey(K)
THEN
RESPONSE #100
StartCutSceneMode()
StartCutScene("P_AlenF")
END

IF
Global("P_Alen_UpdateK","GLOBAL",1)
THEN
RESPONSE #100
Wait (6)
SetGlobal("P_Alen_UpdateK","GLOBAL",0)
END

/*
IF
Global("P_AlenUpdateKit","GLOBAL",0)
THEN
RESPONSE #100
AddKit("P_FMAS")
SetGlobal("P_AlenUpdateKit","GLOBAL",1)
END
*/

// FURIA por muerte de Fall
IF
  InParty("P_Fall") 
  !Dead("P_Fall")
  Global("P_AlenFuryP_Fall","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("P_AlenFuryP_Fall","GLOBAL",0)
END

IF
  IfValidForPartyDialogue(Myself)
  Global("P_AlenFuryP_Fall","GLOBAL",0)
  Dead("P_Fall")
THEN
  RESPONSE #100
    SetGlobal("P_AlenFuryP_Fall","GLOBAL",1)
    DisplayStringHead(Myself,@700042)
	ReallyForceSpellRES("P_SW05",LastAttackerOf("P_Fall"))
	ReallyForceSpellRES("P_SW04",LastAttackerOf("P_Fall"))
	ReallyForceSpellRES("P_SW06",LastAttackerOf("P_Fall"))
	//Attack(LastAttackerOf("P_Fall"))
END

// Alendir Music
IF
InParty("P_Alen")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(2)
Global("P_Alen_Quest1","GLOBAL",1)
Global("P_Alen_Quest1","GLOBAL",3)
//Global("P_Alen_Quest1","GLOBAL",5)
THEN
RESPONSE #100
PlaySong (4444)
StartDialogueNoSet(Player1)
END

// 1st Talk
IF
InParty(Myself)
See(Player1)
PartyHasItem("P_FZAFSW")
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
!AreaCheck("AR0511")
!AreaCheck("P_FAR1")
!AreaCheck("P_FAR2")
!AreaCheck("P_FAR3")
!AreaCheck("P_FA3B")
CombatCounter(0)
!See([ENEMY])
Global("P_Alen_Quest1","GLOBAL",0)
THEN
  RESPONSE #100
  SmallWait (1)
  SetGlobal("P_Alen_Quest1","GLOBAL",1)
END

// Timer
IF 
InParty(Myself)
Global("P_Alen_Quest1","GLOBAL",2)
Global("P_Alen_TalkQuest1","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("P_AlenSoATimer","GLOBAL",60) // TTT 1250
SetGlobal("P_Alen_TalkQuest1","GLOBAL",1)
END

IF
InParty(Myself)
RealGlobalTimerExpired("P_AlenSoATimer","GLOBAL")
See(Player1)								
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("P_Alen_TalkQuest1","GLOBAL",1)
Global("P_Alen_Quest1","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("P_Alen_Quest1","GLOBAL",3)
END

// Alendir Sees Arconti P_FA3B
IF
See("p_arcont")
AreaCheck("P_FA3B")
Global("P_Alen_Quest1","GLOBAL",4)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END